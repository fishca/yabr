#Область ОбработчикиЭлементовФормы

// Процедура - обработка начала выбора каталога
//
&НаКлиенте
Процедура ПутьККаталогуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Каталог журнала регистрации 1С";

	ЗавершениеВыбораФайла = Новый ОписаниеОповещения("ПутьККаталогуНачалоВыбораЗавершение", ЭтаФорма);
	
	Диалог.Показать(ЗавершениеВыбораФайла);
	
КонецПроцедуры // ПутьКФайлуНачалоВыбора()

// Процедура - продолжение обработки выбора каталога
//
&НаКлиенте
Процедура ПутьККаталогуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(ВыбранныеФайлы) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПутьККаталогу = ВыбранныеФайлы[0];
	
	ПутьККаталогуПриИзменении(Элементы.ПутьККаталогу);
	
КонецПроцедуры // ПутьККаталогуНачалоВыбораЗавершение()

// Процедура - обработка изменения каталога
//
&НаКлиенте
Процедура ПутьККаталогуПриИзменении(Элемент)
	
	Элементы.ПозицияЧтения.Заголовок = ОписаниеНачальнойПозицииЧтения(ПутьККаталогу);
	
КонецПроцедуры // ПутьККаталогуПриИзменении()

&НаКлиенте
Процедура РазмерПакетаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	РазмерПакета = РазмерПакета + Направление * 100;
	
КонецПроцедуры // РазмерПакетаРегулирование()

// Функция - получает описание начальной позиции чтения данных
//
// Параметры:
//  ПутьККаталогу	 - Строка - путь к каталогу журнала регистрации
// 
// Возвращаемое значение:
//   Строка - описание начальной позиции чтения
//
&НаСервере
Функция ОписаниеНачальнойПозицииЧтения(ПутьККаталогу)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПозицияЧтения = ОбработкаОбъект.ПолучитьНачальнуюПозициюЧтения(ПутьККаталогу);
	
	Если ПустаяСтрока(ПозицияЧтения.ИмяФайла) Тогда
		Возврат "Все файлы";
	Иначе
		Возврат ПозицияЧтения.ИмяФайла + ":" + Формат(ПозицияЧтения.НомерСтроки, "ЧН=; ЧГ=0");
	КонецЕсли;
	
КонецФункции // ОписаниеНачальнойПозицииЧтения()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "Прочитать"
//
&НаСервере
Функция ПрочитатьНаСервере(ПутьККаталогу)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект.НастройкиСоединения = Новый Структура();
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_Сервер", АдресЭластик);
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_АдресРесурса", "");
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_Пользователь", "");
	ОбработкаОбъект.НастройкиСоединения.Вставить("Сервис_Пароль", "");
	ОбработкаОбъект.НастройкиСоединения.Вставить("УстановленноеСоединение", Неопределено);
	ОбработкаОбъект.НастройкиСоединения.Вставить("УстановленноеСоединение_Использование", Истина);

	ОбработкаОбъект.ЗаголовокИндекса = НРег(Клиент + "_" + ИмяБазы);
	ОбработкаОбъект.РазмерПакетаДляОтправки = РазмерПакета;

	ОбработкаОбъект.ПрочитатьКаталог(ПутьККаталогу);
	
	Возврат ОбработкаОбъект.ПолучитьРезультат(Истина);
	
КонецФункции // ПрочитатьНаСервере()

// Процедура - обработчик команды "Прочитать"
//
&НаКлиенте
Процедура Прочитать(Команда)
	
	Результат = ПрочитатьНаСервере(ПутьККаталогу);
	
КонецПроцедуры // Прочитать()

#КонецОбласти

